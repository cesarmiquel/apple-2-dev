;@com.wudsn.ide.asm.hardware=APPLE2 
; ACME 0.90
; BOBS
; Routine Fast Load (c) 1986 - SAM /EA
; adaptée pour ACME 0.90 - 2014

	*= $FA00 ; ORG = $FA00 (RAM Langage)
                          ;
                          ; HYPER BOOT VERSION 2
                          ; AMELIOREE V-2.1
                          ; SAM
                          ; 30 JUILLET 86
                          ;
                          ;
                          ;
                          ;
                          ;======PARAMETRES DU FAST LOAD
                          ;
                          ;-PARAMETRES D'ENTREE
PISDEP   =   $300       ;PISTE DE DEPART
SECDEP   =   $301       ;SECTEUR DE DEPART
BUFFER   =   $302       ;ADRESSE OU L'ON CHARGE
TOTSEC   =   $304       ;TOTAL DES SECTEURS A CHARGER
DRVNUM   =   $305       ;1 OU 2
ERRMAX   =   $306       ;MAXIMUM D'ERREURS TOLEREES SUR UN SECTEUR AVANT DE RENDRE LA MAIN
DLFLAG   =   $307       ;1=DELAI AVANT DE LIRE ;0=NON
                          ;
                          ;
                          ;-PARAMETRES DE SORTIE
ERRNUM   =   $308       ;CODE ERREUR
SECERR   =   $309       ;SECTEUR OU S'EST PRODUIT L'ERREUR (S'IL Y EN A UN!)
PISERR   =   $30A       ;PISTE IDEM
                          ;
                          ;-PARAMETRES ENTREE/SORTIE
ICURTRK1 =   $310       ;2*PISTE INITIALE DU DRIVE1 A GARNIR POUR LE 1ER APPEL
ICURTRK2 =   $311       ;2*PISTE INITIALE DU DRIVE2    "
                          ;
                          ;
                          ;
                          ;======ADRESSES HARD-SYSTEM
DRVSM0   =   $C0E0
DRVSM1   =   $C0E1
DRVSM2   =   $C0E2
DRVSM4   =   $C0E4
DRVSM6   =   $C0E6
DRVOFF   =   $C0E8
DRVON    =   $C0E9
DRVRD    =   $C0EC
DRVRDM   =   $C0EE
DRVSL1   =   $C0EA
                          ;
                          ;
                          ;======PAGE ZERO PROGRAMME
                          ;
INTER    =   $0           ;[$AA,$FF] UTILISE EN FAIT
INTER2	 =   $FE		  ; (modification pour ACME)
ACCA     =   $A9
ACCB     =   $A8

RND		= 	 $0C
RND2	=	 $0D
                          ;
                          ;
                          ;
                          ;
                          ;
                          ;
                          ;======PROGRAMME
                          ;
         JMP   LITDIS
JMPSEC   RTS              ;A CHANGER EN JMP EVENTUELLEMENT
         !fill    2
JMPPIS   RTS              ;IDEM
         !fill    2
                          ;
                          ;======LIT/DECODE LE SECTEUR
                          ;<ALGORITHME V2,PLUS FIABLE QUE V1,EXTRAIT DU DOS E.A.>
                          ;S:ERRNUM
                          ;
LITSEC   LDX   #0
         LDY   #10
         JMP   LITSEC11
                          ;
LITSEC10 INX
         BNE   LITSEC11
         DEY
         BNE   LITSEC11
                          ;
                          ;-ERREUR:PAS D'EN TETE DONNEES
         LDA   #2
         STA   ERRNUM
         RTS
                          ;
LITSEC11 LDA   DRVRD
         BPL   LITSEC11
         CMP   #$D5
         BNE   LITSEC10
LITSEC8  LDA   DRVRD
         BPL   LITSEC8
         CMP   #$AA
         BNE   LITSEC10
LITSEC9  LDA   DRVRD
         BPL   LITSEC9
         CMP   #$AD
         BNE   LITSEC10
                          ;
         LDA   #0
         LDX   #0-$56
LITSEC1  STA   ACCA
LITSEC7  LDY   DRVRD
         BPL   LITSEC7
         LDA   TABDEC,Y
         STA   INTER,X
         EOR   ACCA
         INX
         BNE   LITSEC1
                          ;
         LDX   #0-$56
         JMP   LITSEC2
BUF1     STA   $FFFF,X
LITSEC2  LDY   DRVRD
         BPL   LITSEC2
         EOR   TABDEC,Y
         LDY   INTER,X
         EOR   FONC1,Y
         INX
         BNE   BUF1
         PHA
                          ;
         AND   #$FC
                          ;
         LDX   #0-$56
LITSEC3  LDY   DRVRD
         BPL   LITSEC3
         EOR   TABDEC,Y
         LDY   INTER,X
         EOR   FONC2,Y
BUF2     STA   $FFFF,X
         INX
         BNE   LITSEC3
         AND   #$FC
                          ;
LITSEC13 LDY   DRVRD
         BPL   LITSEC13
         LDX   #0-$54
LITSEC12 EOR   TABDEC,Y
         LDY   INTER2,X
         EOR   FONC3,Y
BUF3     STA   $FFFF,X
LITSEC4  LDY   DRVRD
         BPL   LITSEC4
         INX
         BNE   LITSEC12
         AND   #$FC
                          ;
         EOR   TABDEC,Y
         BEQ   LITSEC6
                          ;
                          ;-ERREUR DE CHECKSUM
         LDA   #1
         STA   ERRNUM
                          ;
LITSEC6  LDA   BUF1+1
         STA   LITSEC14+1
         LDA   BUF1+2
         CLC
         ADC   #1
         STA   LITSEC14+2
         PLA
LITSEC14 STA   $FFFF
         RTS
                          ;
                          ;======TIENS,SI JE METTAIS MON NOM?
         !byte   0-"S"
         !byte   0-"A"
         !byte   0-"M"
                          ;
                          ;======CALCULE LES TABLES DE FONCTIONS
CALFON   LDY   #0
CALFON1  TYA
         ASL  
         ASL  
         TAX
                          ;
         LDA   #0
         STA   FONC1,X
         STA   FONC2,X
         STA   FONC3,X
                          ;
         TYA
         LSR  
         ROL   FONC1,X
         LSR  
         ROL   FONC1,X
                          ;
         LSR  
         ROL   FONC2,X
         LSR 
         ROL   FONC2,X
                          ;
         LSR  
         ROL   FONC3,X
         LSR  
         ROL   FONC3,X
                          ;
         INY
         CPY   #$40
         BNE   CALFON1
         RTS
                          ;
                          ;======LIT LE DISQUE
                          ;S:ERRNUM,SECERR,PISERR
LITDIS   LDA   FLGTRK
         BNE   LITDIS4
         LDA   ICURTRK1
         STA   CURTRK1
         LDA   ICURTRK2
         STA   CURTRK2
         LDA   #1
         STA   FLGTRK
                          ;
LITDIS4  JSR   INILEC
         JSR   CALFON
         JSR   CALBUF
         JSR   CALBOR
         LDA   PISDEP
         STA   PISTE
                          ;
LITDIS1  JSR   ARMOVE
         JSR   CALLIM
         JSR   LITPIS
         LDA   ERRNUM
         BNE   LITDIS3
                          ;
         LDA   PISTE
         CMP   PISFIN
         BEQ   LITDIS2
         INC   PISTE
         JMP   LITDIS1
                          ;
LITDIS3  LDA   PISTE
         STA   PISERR
LITDIS2  LDA   DRVOFF
         RTS
                          ;
                          ;======LIT UNE PISTE
                          ;E:NBSEC,SMIN,SMAX,BASE1,BASE2,BASE3,PISTE
                          ;S:BASE1,BASE2,BASE3,ERRNUM,SECERR
                          ;
LITPIS   LDA   NBSEC
         STA   COUNT
                          ;
         LDX   #15
         LDA   #0
LITPIS7  STA   TMARSEC,X
         DEX
         BPL   LITPIS7
                          ;
LITPIS1  LDA   ERRMAX
         STA   ERRCNT
LITPIS6  LDA   #0
         STA   ERRNUM
                          ;
         JSR   LOCSEC
         LDA   ERRNUM
         BEQ   LITPIS3
         LDA   ERRCNT
         BEQ   LITPIS2
         DEC   ERRCNT
         LDA   ERRNUM
         CMP   #4
         BNE   LITPIS6
         JSR   RECAL
         JMP   LITPIS6
                          ;
LITPIS2  LDA   SECTOR
         STA   SECERR
         RTS
                          ;
LITPIS3  LDY   SECTOR
         CPY   SMIN
         BCC   LITPIS1
         CPY   SMAX
         BCS   LITPIS1
                          ;
         LDA   TMARSEC,Y
         BNE   LITPIS1
                          ;
         LDA   SECTOR
         SEC
         SBC   SMIN
         TAY
         CLC
         ADC   BASE1
         STA   BUF1+2
         TYA
         CLC
         ADC   BASE2
         STA   BUF2+2
         TYA
         CLC
         ADC   BASE3
         STA   BUF3+2
                          ;
         JSR   LITSEC
         LDA   ERRNUM
         BEQ   LITPIS5
         LDA   ERRCNT
         BEQ   LITPIS2
         DEC   ERRCNT
         JMP   LITPIS6
                          ;
LITPIS5  LDY   SECTOR
         LDA   #1
         STA   TMARSEC,Y
                          ;
         JSR   JMPSEC
                          ;
         DEC   COUNT
         BEQ   LITPIS8
         JMP   LITPIS1
                          ;
LITPIS8  JSR   JMPPIS
                          ;
         LDA   BASE1
         CLC
         ADC   NBSEC
         STA   BASE1
         LDA   BASE2
         CLC
         ADC   NBSEC
         STA   BASE2
         LDA   BASE3
         CLC
         ADC   NBSEC
         STA   BASE3
         RTS
                          ;
TMARSEC  !fill    16
COUNT    !fill    1
ERRCNT   !fill    1
                          ;
                          ;======ERREUR DE PISTE:RECALIBRAGE
RECAL    LDA   PISTE
         STA   SAVPIS
                          ;
         LDA   #48
         LDX   DRVNUM
         STA   CURTRK1-1,X
         LDA   #0
         STA   PISTE
         JSR   ARMOVE
                          ;
         LDA   SAVPIS
         STA   PISTE
         JSR   ARMOVE
         RTS
                          ;
SAVPIS   !fill    1
                          ;
                          ;======CALCULE LES LIMITES DE LA PISTE
                          ;E:PISTE,PISDEP,PISFIN,SECDEP,SECFIN
                          ;S:SMIN,SMAX,NBSEC
CALLIM   LDA   #$0
         STA   SMIN
         LDA   #$10
         STA   SMAX
                          ;
         LDA   PISTE
         CMP   PISDEP
         BNE   CALLIM1
         LDA   SECDEP
         STA   SMIN
CALLIM1  LDA   PISTE
         CMP   PISFIN
         BNE   CALLIM2
         LDA   SECFIN
         STA   SMAX
                          ;
CALLIM2  LDA   SMAX
         SEC
         SBC   SMIN
         STA   NBSEC
         RTS
                          ;
SMIN     !fill    1
SMAX     !fill    1
NBSEC    !fill    1
PISTE    !fill    1
                          ;
                          ;======CALCULE LES BORNES DU DISQUE
                          ;E:PISDEP,SECDEP,TOTSEC
                          ;S:PISFIN,SECFIN
                          ;
CALBOR   LDA   PISDEP
         STA   CALBORA
         LDA   #0
         LDX   #4
CALBOR1  LSR   CALBORA
         ROR  
         DEX
         BNE   CALBOR1
                          ;
         ORA   SECDEP
         CLC
         ADC   TOTSEC
         STA   CALBORB
         AND   #$F
         STA   SECFIN
         LDA   CALBORA
         ADC   #0
                          ;
         LDX   #4
CALBOR2  ASL   CALBORB
         ROL  
         DEX
         BNE   CALBOR2
         STA   PISFIN
                          ;
         LDA   SECFIN
         BNE   CALBOR3
         LDA   #$10
         STA   SECFIN
         DEC   PISFIN
                          ;
CALBOR3  RTS
                          ;
CALBORA  !fill    1
CALBORB  !fill    1
                          ;
PISFIN   !fill    1
SECFIN   !fill    1
                          ;
                          ;======CALCULE LES BUFFERS
                          ;E:BUFFER^,SECDEP
                          ;S:BUF1+1,BUF2+1,BUF3+1,BASE1,BASE2,BASE3
                          ;
CALBUF   LDA   BUFFER
         STA   BUF3+1
         SEC
         SBC   #$AB
         STA   BUF1+1
         LDA   BUFFER+1
         SBC   #0
         STA   BASE1
                          ;
         LDA   BUFFER+1
         STA   BASE3
                          ;
         LDA   BUFFER
         SEC
         SBC   #$54
         STA   BUF2+1
         LDA   BUFFER+1
         SBC   #0
         STA   BASE2
         RTS
                          ;
BASE1    !fill    1
BASE2    !fill    1
BASE3    !fill    1
                          ;
                          ;======ARMOVE
                          ;E:PISTE,CURTRK1,CURTRK2,DRVNUM
                          ;S:CURTRK1,CURTRK2
                          ;
ARMOVE   LDX   DRVNUM
         LDA   PISTE
         ASL   
         STA   ACCA
ARMOVE1  LDA   CURTRK1-1,X
         STA   ACCB
         SEC
         SBC   ACCA
         BEQ   ARMOVE2
         BCS   ARMOVE3
         INC   CURTRK1-1,X
         BCC   ARMOVE4
ARMOVE3  DEC   CURTRK1-1,X
ARMOVE4  JSR   ARMOVE5
         JSR   ARMOVE6
         LDA   ACCB
         AND   #3
         ASL  
         ORA   #0         ;POUR RESPECTER LE TIMING
         TAY
         LDA   DRVSM0,Y
         JSR   ARMOVE6
         BEQ   ARMOVE1
ARMOVE5  LDA   CURTRK1-1,X
         AND   #3
         ASL  
         ORA   #0         ;IDEM
         TAY
         LDA   DRVSM1,Y
ARMOVE2  RTS
ARMOVE6  LDA   #$28
TEMP     SEC
ARMOVE7  PHA
ARMOVE8  SBC   #1
         BNE   ARMOVE8
         PLA
         SBC   #1
         BNE   ARMOVE7
         RTS
                          ;
CURTRK1  !fill    1
CURTRK2  !fill    1
FLGTRK   !byte   0
                          ;
                          ;======INIT LECTURE
INILEC   LDA   DRVON
         LDX   DRVNUM
         LDA   DRVSL1-1,X
         LDA   DRVRDM     ;MODE
         LDA   DRVRD      ;LECTURE
                          ;
         LDA   DRVSM0
         LDA   DRVSM2
         STA   RND2
         LDA   DRVSM4
         LDA   DRVSM6     ;INIT PHASES POUR BRAS
         STA   RND
                          ;
         LDA   #1
         JSR   TEMP
                          ;
         LDA   DLFLAG
         BEQ   INILEC1
                          ;
         LDY   #3
INILEC2  LDA   #0
         JSR   TEMP
         DEY
         BNE   INILEC2
                          ;
INILEC1  RTS
                          ;
                          ;======LOCALISE UN SECTEUR
                          ;
                          ;E:PISTE
                          ;S:SECTOR,ERRNUM
                          ;
LOCSEC   LDX   #0
         LDY   #10
         JMP   LOCSEC11
                          ;
LOCSEC10 INX
         BNE   LOCSEC11
         DEY
         BNE   LOCSEC11
                          ;
                          ;-ERREUR:PAS D'EN TETE ADRESSE
         LDA   #3
         STA   ERRNUM
         RTS
                          ;
LOCSEC11 LDA   DRVRD
         BPL   LOCSEC11
         CMP   #$D5
         BNE   LOCSEC10
LOCSEC1  LDA   DRVRD
         BPL   LOCSEC1
         CMP   #$AA
         BNE   LOCSEC10
LOCSEC2  LDA   DRVRD
         BPL   LOCSEC2
         CMP   #$96
         BNE   LOCSEC10
                          ;
         LDY   #0
LOCSEC4  LDA   DRVRD
         BPL   LOCSEC4
         STA   LOCSECA
LOCSEC5  LDA   DRVRD
         BPL   LOCSEC5
         SEC
         ROL   LOCSECA
         AND   LOCSECA
         STA   TENTETE,Y
         INY
         CPY   #3
         BNE   LOCSEC4
                          ;
         LDX   SECPHY
         LDA   TSECT,X
         STA   SECTOR
                          ;
         LDA   TRACK
         CMP   PISTE
         BEQ   LOCSEC6
                          ;
                          ;-ERREUR:LA PISTE NE CORESPOND PAS
         LDA   #4
         STA   ERRNUM
                          ;
LOCSEC6  RTS
                          ;
TSECT    !byte   0,7,$E,6,$D,5,$C,4,$B,3,$A,2,9,1,8,$F
LOCSECA  !fill    1
SECTOR   !fill    1
                          ;
TENTETE
VOLUME   !fill    1
TRACK    !fill    1
SECPHY   !fill    1
                          ;
                          ;
                          ;======TABLES UTILISEES POUR LE DECODAGE
                          ;
FONC1    !fill    258        ;SERA GARNIE EN TEMPS UTILE
FONC2    =   FONC1+1
FONC3    =   FONC2+1
                          ;
TABDEC
         !fill    $96
FTABDEC  !byte   $00,$04
         !byte   $FC
         !byte   $FC,$08,$0C
         !byte   $FC,$10,$14,$18
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC,$1C,$20
         !byte   $FC
         !byte   $FC
         !byte   $FC,$24,$28,$2C,$30,$34
         !byte   $FC
         !byte   $FC,$38,$3C,$40,$44,$48,$4C
         !byte   $FC,$50,$54,$58,$5C,$60,$64,$68
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC,$6C
         !byte   $FC,$70,$74,$78
         !byte   $FC
         !byte   $FC
         !byte   $FC,$7C
         !byte   $FC
         !byte   $FC,$80,$84
         !byte   $FC,$88,$8C,$90,$94,$98,$9C,$A0
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC
         !byte   $FC,$A4,$A8,$AC
         !byte   $FC,$B0,$B4,$B8,$BC,$C0,$C4,$C8
         !byte   $FC
         !byte   $FC,$CC,$D0,$D4,$D8,$DC,$E0
         !byte   $FC,$E4,$E8,$EC,$F0,$F4,$F8
         !byte   $FC